// SPDX-FileCopyrightText: 2019 Serokell <https://serokell.io>
//
// SPDX-License-Identifier: MPL-2.0

library ContractMessages  // Prepare signed messages for the contract

"TonUtil.fif" include
"State/GlobalState.fif" include

{ <b 1 4 u, b> } : mkWaitingBoth

{
  // stack: (globalState: Cell) (wc: Int) (code: Cell)
  rot
  // wc, code, globalState
  <b swap ref, mkWaitingBoth ref, b>
  // wc, code, state
  null // no libraries
  // Libs{ x{ABACABADABACABA} drop x{AAAA} s>c public_lib x{1234} x{5678} |_ s>c public_lib }Libs
  <b b{0011} s, 3 roll ref, rot ref, swap dict, b>  // create StateInit
  // wc, stateInit
  dup hashu
  // wc, stateInit, hash
  rot swap
  // wc, hash, stateInit
  // stack: (stateInit: Cell) (contractAddr: (int, int))
} : createStateInit

{
<b
  b{01} s, // internalMsgTag ihr_disabled
  b{1} s, // bounce
  b{000100} s, // 0 - boundced 00 - msgAddrNone 10 - msgAddrStd 0 - anycast = nothing
  5 roll 5 roll addr,
  3 roll Gram,
  // 9 - empty dict \ ihr_fee = 0 \ fwd_fee = 0 \ created_lt = 0 \created_at = 0
  // leaving all zeros, will be filled by SENDRAWMSG
  0 9 64 32 + + u,
  b{11} s, // init = Just Right
  2 roll ref, // init ref
  b{1} s, // body = right
  swap ref, // body
b>
} : createInitMessage
// ( wc addr amount init body -- msg )

{
<b
  b{01} s, // internalMsgTag ihr_disabled
  b{1} s, // bounce
  b{000100} s, // 0 - boundced 00 - msgAddrNone 10 - msgAddrStd 0 - anycast = nothing
  4 roll 4 roll addr,
  2 roll Gram,
  // 9 - empty dict \ ihr_fee = 0 \ fwd_fee = 0 \ created_lt = 0 \created_at = 0
  // leaving all zeros, will be filled by SENDRAWMSG
  0 9 64 32 + + u,
  b{0} s, // init = Nothing
  b{1} s, // body = Right
  swap ref, // STM
b>
} : createMsg
// ( wc addr amount body -- msg )

{ <b
    5 roll 32 u,
    4 roll 1 u,
    3 roll 3 roll addr,
    swap ref,
  b> dup hashu
  2 roll ed25519_sign_uint
  // ( cell sign )
  swap <b swap <s s, swap B, b>
} : mkRawReqMsg
// ( sk opcode pkn wc addr payload -- rawMsg )

{ 1 3 -roll <b b> mkRawReqMsg } : mkJoinMsgBody
// ( sk pkn wc addr -- cell )
{ 2 6 -roll <b 3 roll 121 i, -rot { ref, } if b> mkRawReqMsg } : mkCloseMsgBody
// ( sk pkn wc addr payout miou bool -- cell )
{ 3 3 -roll <b b> mkRawReqMsg } : mkTimeoutMsgBody
// ( sk pkn wc addr -- cell )
